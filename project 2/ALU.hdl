// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    And16(a=x, b=false, out=andX);
    Mux16(a=x, b=andX, sel=zx, out=firstFuncResultX);
    Not16(in=firstFuncResultX, out=notFirstFuncResultX);
    Mux16(a=firstFuncResultX, b=notFirstFuncResultX, sel=nx, out=secondFuncResultX);
    And16(a=y, b=false, out=andY);
    Mux16(a=y, b=andY, sel=zy, out=firstFuncResultY);
    Not16(in=firstFuncResultY, out=notFirstFuncResultY);
    Mux16(a=firstFuncResultY, b=notFirstFuncResultY, sel=ny, out=secondFuncResultY);
    Add16(a=secondFuncResultX, b=secondFuncResultY, out=fifthFuncAddXY);
    And16(a=secondFuncResultX, b=secondFuncResultY, out=fifthFuncAndXY);
    Mux16(a=fifthFuncAndXY, b=fifthFuncAddXY, sel=f, out=fifthFuncResult);
    Not16(in=fifthFuncResult, out=sixthFuncResult);
    Mux16(a=fifthFuncResult, b=sixthFuncResult, sel=no, out[15]=MSBOfComputation, out[0..7]=low8, out[8..15]=high8, out=out);
    And(a=MSBOfComputation, b=true, out=ng);
    Or8Way(in=low8, out=low8Or);
    Or8Way(in=high8, out=high8Or);
    Or(a=low8Or, b=high8Or, out=finalOr);
    Not(in=finalOr, out=zr);

}